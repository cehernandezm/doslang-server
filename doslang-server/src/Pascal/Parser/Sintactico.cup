package Pascal.Parser;

import java_cup.runtime.*;
import Pascal.Analisis.*;
import Pascal.Analisis.TipoDato.*;
import Pascal.Componentes.*;
import Pascal.Componentes.UserTypes.*;
import Pascal.Componentes.Arreglos.*;
import Pascal.Componentes.Registros.*;
import Pascal.Componentes.SentenciaIf.*;
import Pascal.Componentes.Funciones.*;
import java.util.LinkedList;
import Pascal.Componentes.SentenciaSwitch.*;

parser code
{:

	public static LinkedList<Instruccion> lista;

	

	public  void setLista(LinkedList<Instruccion> listaG){
		lista = listaG;
	}

	public  LinkedList<Instruccion> getLista(){
		return lista;
	}
	
	public static int cont=0;
	public void syntax_error(Symbol s){
        System.err.println("Error Sintáctico en la Línea " + (s.left) +" Columna "+s.right+ ". No se esperaba este componente: " +s.value+".");
	}

	 public void unrecovered_syntax_error(Symbol s) throws java.lang.Exception{
        System.err.println("Error síntactico irrecuperable en la Línea " + (s.left)+ " Columna "+s.right+". Componente " + s.value + " no reconocido.");
    }
:}

terminal UMENOS;
terminal MAS,MENOS,POR,DIVIDIDO,MODULO,POTENCIA,PNT;
terminal SALUDO,ID,ENTERO,DECIMAL,NIL;
terminal PARIZQ,PARDER,PNTCOMA,DSPUNTOS,COMA;
terminal LLAVEIZQ,LLAVEDER;

terminal CHAR,CADENA,TRUE,FALSE;

terminal MAYOR,MENOR,MAYORIGUAL,MENORIGUAL, IGUAL, DIFERENTE;
terminal AND, OR, NAND, NOR,NOT;
terminal RNOT;

terminal PROGRAM,CONST,TYPE,VAR,BEGIN,END;

terminal INTEGER,BOOLEAN,REAL,STRING,WORD,NILL,CHARP;

terminal WRITELN,WRITE;

terminal CHARAT,LENGTH,REPLACE,TOLOWERCASE,TOUPPERCASE,EQUALS,TRUNK,ROUND;

terminal ARRAY,OF;

terminal RECORD,SIZEOF,MALLOC,FREE;

terminal IF,THEN,ELSE;

terminal CASE,DEFAULT;

terminal WHILE, DO;

terminal REPEAT,UNTIL;

terminal FOR,TO,DOWNTO;

terminal READ;

terminal FUNCTION,PROCEDURE;

terminal BREAK, CONTINUE,EXIT;

non terminal LinkedList<Instruccion> inicio;

non terminal Expresion expresion;

non terminal insNombrePrograma;

non terminal Type tipoDato;
non terminal LinkedList<String> listaID;

//------------------------------------------------------------ NO TERMINALES PARA CONSTANTES ---------------------------------------
non terminal LinkedList<Instruccion> listaDeclaracionConstantes;
non terminal Instruccion declaracionConstante;
non terminal LinkedList<Instruccion> insConstantes;
//------------------------------------------------------------ NO TERMINALES PARA USERTYPES --------------------------------------------
non terminal LinkedList<Instruccion> insType;
non terminal TypeDeclaration equivalencia;
non terminal LinkedList<Instruccion> listaType;
non terminal Dimension insDimen;
non terminal Enumerador insEnum;
non terminal Instruccion tipoType;
//----------------------------------------------------------- NO TERMINALES PARA VARIABLES -------------------------------------
non terminal LinkedList<Instruccion> insVar;
non terminal LinkedList<Instruccion> listaDeclaracionVariables;
non terminal Instruccion declaracionVariable;

//------------------------------------------------------------ NO TERMINALES PARA CUERPO DE PROGRAMA -----------------------------
non terminal LinkedList<Instruccion> instruccionesPrincipal;
non terminal Instruccion instruccionPrincipal;
non terminal Instruccion instruccionOneLine;
non terminal LinkedList<Instruccion> cuerpoPrincipal;

//---------------------------------------------------------- NO TERMINALES PARA WRITELN -----------------------------------------------
non terminal Writeln insWrite;
non terminal Expresion listaExpresiones;

//---------------------------------------------------------- NO TERMINALES PARA ASIGNACION ---------------------------------------------
non terminal Instruccion insAsignacion;

//--------------------------------------------------------- NO TERMINALES PARA ARREGLOS -----------------------------------------------
non terminal Type insArray;
non terminal LinkedList<Dimension> dimensionList;
non terminal Dimension dimension;
non terminal LinkedList<Expresion> listaAcceso;

//--------------------------------------------------------- NO TERMINALES PARA REGISTROS ----------------------------------------------
non terminal Type insRegistro;
non terminal LinkedList<Atributo> listaAtributos;
non terminal Free insFree;

//--------------------------------------------------------- NO TERMINALES PARA IF -----------------------------------------------------
non terminal IfSuperior ifSuperior;
non terminal LinkedList<Instruccion> insIf;
non terminal LinkedList<IF> listadoElseIf;


//--------------------------------------------------------- NO TERMINALES PARA SWITCH ---------------------------------------------------
non terminal SWITCH insSwitch;
non terminal LinkedList<CASE> listaCase;
non terminal CASE insCase;
non terminal CASE insDefault;

//---------------------------------------------------------- NO TERMINALES PARA WHILE ----------------------------------------------------
non terminal WHILE insWhile;

//---------------------------------------------------------- NO TERMINALES PARA EL REPEAT ------------------------------------------------
non terminal REPEAT insRepeat;

//---------------------------------------------------------- NO TERMINALES PARA FOR ------------------------------------------------------
non terminal FOR insFor;

//---------------------------------------------------------- NO TERMINALES PARA READ -----------------------------------------------------
non terminal Read insRead;


//---------------------------------------------------------- NO TERMINALES PARA FUNCIONES/ PROCEDURES ----------------------------------------------
non terminal LinkedList<Instruccion> insFunciones;
non terminal LinkedList<Instruccion> listaFunciones;
non terminal Funcion insFuncion;
non terminal LinkedList<Parametro> listaParametros;
non terminal Parametro parametro;
non terminal Funcion insProcedure;
non terminal Expresion llamadaFuncion;

//---------------------------------------------------------- NO TERMINALES PARA BREAK ----------------------------------------------------------
non terminal Break insBreak;

//--------------------------------------------------------- NO TERMINALES PARA CONTINUE --------------------------------------------------------
non terminal Continue insContinue;

//---------------------------------------------------------- NO TERMINALEX PARA EXIT ------------------------------------------------------------
non terminal Exit insExit;


non terminal LinkedList<Instruccion> insOneLine;

//----------------------------------------------------------------------- PRECEDENCIA RELACIONALES ----------------------------------------------------
precedence left OR;
precedence left NAND, NOR;
precedence left AND;
precedence right RNOT;
precedence left MAYOR,MENOR,MAYORIGUAL,MENORIGUAL, IGUAL,DIFERENTE;

//----------------------------------------------------------------------- PRECEDENCIA OPERADORES ------------------------------------------------------
precedence left MAS,MENOS;
precedence left POR,DIVIDIDO,MODULO;
precedence left POTENCIA;
precedence left PNT,LLAVEIZQ;
precedence right UMENOS;

precedence left ELSE;






start with inicio;


inicio ::= insNombrePrograma insType:a	cuerpoPrincipal:b					{: RESULT = new LinkedList<Instruccion>(); RESULT.addAll(a); RESULT.addAll(b); setLista(RESULT); :}
		 | insNombrePrograma cuerpoPrincipal:a 								{: RESULT = new LinkedList<Instruccion>(); RESULT.addAll(a); setLista(RESULT); :}	 
         ;




//------------------------------------------------------------ NOMBRE DE UN PROGRAMA ---------------------------------------------------------
insNombrePrograma ::= PROGRAM ID PNTCOMA
                    ;

//--------------------------------------------------------- CONSTANTE | CONSTANTE VAR ---------------------------------------------------------
insConstantes ::= CONST listaDeclaracionConstantes:a 					{: RESULT = a; :}
				| CONST listaDeclaracionConstantes:a insVar:b 			{: RESULT = a; RESULT.addAll(b); :}
				| insVar:a 							                    {: RESULT = a; :}
				;

listaDeclaracionConstantes ::= listaDeclaracionConstantes:a declaracionConstante:b          {: RESULT = a; RESULT.addLast(b); :} 
						     | declaracionConstante:a 									    {: RESULT = new LinkedList<Instruccion>(); RESULT.addLast(a);  :}
						     ;


declaracionConstante ::= listaID:a DSPUNTOS tipoDato:b IGUAL expresion:c PNTCOMA   			{: RESULT = new Declaracion(c,bleft,bright,true,b,a); :}
					   | listaID:a DSPUNTOS tipoDato:b PNTCOMA   			                {: RESULT = new Declaracion(null,bleft,bright,true,b,a); :}
					   ;

//--------------------------------------------------------- VAR | VAR CUERPO ----------------------------------------------------------------

insVar ::= VAR listaDeclaracionVariables:a 							{: RESULT = a; :}
		 | VAR listaDeclaracionVariables:a insFunciones:b           {: RESULT = a; RESULT.addAll(b); :}
		 | insFunciones:a 									        {: RESULT = a; :}
		 ;

listaDeclaracionVariables ::= listaDeclaracionVariables:a declaracionVariable:b          	{: RESULT = a; RESULT.addLast(b); :} 
						    | declaracionVariable:a 									    {: RESULT = new LinkedList<Instruccion>(); RESULT.addLast(a);  :}
						    ;


declaracionVariable ::= listaID:a DSPUNTOS tipoDato:b IGUAL expresion:c PNTCOMA   			{: RESULT = new Declaracion(c,bleft,bright,false,b,a); :}
					  | listaID:a DSPUNTOS tipoDato:b PNTCOMA  								{: RESULT = new Declaracion(null,bleft,bright,false,b,a); :}				
					  ;

//---------------------------------------------------------- FUNCIONES  ------------------------------------------------
insFunciones ::= listaFunciones:a 									{: RESULT = a; :} 	
			   
			   ;  

listaFunciones ::= listaFunciones:a insFuncion:b 						{: RESULT = a; RESULT.addLast(b); :}
				 | listaFunciones:a insProcedure:b 						{: RESULT = a; RESULT.addLast(b); :}
				 | insFuncion:a 										{: RESULT = new LinkedList<>(); RESULT.addLast(a); :}
				 | insProcedure:a 										{: RESULT = new LinkedList<>(); RESULT.addLast(a); :}
				 ;	


insFuncion ::= FUNCTION ID:a PARIZQ listaParametros:b PARDER DSPUNTOS tipoDato:c PNTCOMA insVar:d BEGIN instruccionesPrincipal:e END PNTCOMA  	{: d.addAll(e); RESULT = new Funcion(b,a.toString(),c,d,aleft,aright); :}
			 | FUNCTION ID:a DSPUNTOS tipoDato:b PNTCOMA insVar:c BEGIN instruccionesPrincipal:d END PNTCOMA 									{: c.addAll(d); RESULT = new Funcion(new LinkedList<>(),a.toString(),b,c,aleft,aright); :}
			 | FUNCTION ID:a PARIZQ listaParametros:b PARDER DSPUNTOS tipoDato:c PNTCOMA  BEGIN instruccionesPrincipal:d END PNTCOMA        	{: RESULT = new Funcion(b,a.toString(),c,d,aleft,aright); :}
			 | FUNCTION ID:a DSPUNTOS tipoDato:b PNTCOMA  BEGIN instruccionesPrincipal:c END PNTCOMA 											{: RESULT = new Funcion(new LinkedList<>(),a.toString(),b,c,aleft,aright); :}
			 ;

insProcedure ::= PROCEDURE ID:a PARIZQ listaParametros:b PARDER PNTCOMA insVar:d BEGIN instruccionesPrincipal:e END PNTCOMA  					{: d.addAll(e); RESULT = new Funcion(b,a.toString(),new Type("",Tipo.VOID),d,aleft,aright); :}
			   | PROCEDURE ID:a PNTCOMA insVar:c BEGIN instruccionesPrincipal:d END PNTCOMA 													{: c.addAll(d); RESULT = new Funcion(new LinkedList<>(),a.toString(),new Type("",Tipo.VOID),c,aleft,aright); :}
			   | PROCEDURE ID:a PARIZQ listaParametros:b PARDER PNTCOMA  BEGIN instruccionesPrincipal:d END PNTCOMA        						{: RESULT = new Funcion(b,a.toString(),new Type("",Tipo.VOID),d,aleft,aright); :}
			   | PROCEDURE ID:a PNTCOMA  BEGIN instruccionesPrincipal:c END PNTCOMA 															{: RESULT = new Funcion(new LinkedList<>(),a.toString(),new Type("",Tipo.VOID),c,aleft,aright); :}
			   ;

listaParametros ::= listaParametros:a PNTCOMA parametro:b   	{: RESULT = a; RESULT.addLast(b); :}
				  | parametro:a 						 	{: RESULT = new LinkedList<>(); RESULT.addLast(a); :}
				  ; 

parametro ::= listaID:a DSPUNTOS tipoDato:b 		   {: RESULT = new Parametro(a,false,b); :}
			| VAR listaID:a DSPUNTOS tipoDato:b 	   {: RESULT = new Parametro(a,true,b); :}
			; 

//--------------------------------------------------------- TYPE | TYPE CONSTANTES ---------------------------------------------------------------
insType ::= TYPE listaType:a 										{: RESULT = a; :}
		  | TYPE listaType:a insConstantes:b 				        {: RESULT = a; RESULT.addAll(b); :}
		  | insConstantes:a 				                        {: RESULT = a; :}
		  | error PNTCOMA 											{: RESULT = new LinkedList<Instruccion>(); :}
          ;				

listaType ::= listaType:a equivalencia:b 							{: RESULT = a; RESULT.addLast(b); :}
			| equivalencia:a 										{: RESULT = new LinkedList<>(); RESULT.addLast(a); :}
			;  								   

equivalencia ::= listaID:a IGUAL tipoDato:b PNTCOMA 					{: RESULT = new TypeDeclaration(a,aleft,aright,b); :}
			   ;

insEnum ::= PARIZQ listaID:b PARDER  					{: RESULT = new Enumerador(b,bleft,bright,false); :}
		  ;  




//---------------------------------------------- TIPOS DE DATOS -------------------------------------------------------------------------
tipoDato ::= INTEGER:a 									{: RESULT = new Type(a.toString(),Tipo.INT); :}
		   | BOOLEAN:a 									{: RESULT = new Type(a.toString(),Tipo.BOOLEAN); :}
		   | REAL:a 									{: RESULT = new Type(a.toString(),Tipo.DOUBLE); :}
		   | CHARP:a 									{: RESULT = new Type(a.toString(),Tipo.CHAR); :}
		   | WORD:a 									{: RESULT = new Type(a.toString(),Tipo.WORD); :}
		   | STRING:a 									{: RESULT = new Type(a.toString(),Tipo.STRING); :}
		   | insDimen:a 								{: RESULT = new Type("",Tipo.DIMENSION); RESULT.setValor(a); :}
		   | ID:a 										{: RESULT = new Type(a.toString(),Tipo.ID); :}
		   | insArray:a 								{: RESULT = a; :}
		   | insRegistro:a 								{: RESULT = a; :}
		   | insEnum:a 									{: RESULT = new Type("",Tipo.ENUM); RESULT.setValor(a); :}
		   ;

//------------------------------------------- ARREGLOS -----------------------------------------------------------------------------------

insDimen ::= ENTERO:a PNT PNT ENTERO:b 				    {: RESULT = new Dimension(new Expresion(a,Tipo.INT,aleft,aright),new Expresion(b,Tipo.INT,bleft,bright),aleft,aright); :}
		   | CHAR:a PNT PNT CHAR:b  					{: RESULT = new Dimension(new Expresion(a,Tipo.CHAR,aleft,aright),new Expresion(b,Tipo.CHAR,bleft,bright),aleft,aright); :}
		   ;

insArray ::= ARRAY LLAVEIZQ dimensionList:a LLAVEDER OF tipoDato:b
			{:

				if(b.getTipo() == Tipo.ARRAY){
					Arreglo temp = (Arreglo)b.getValor();
					LinkedList<Dimension> lista = temp.getDimensiones();
					a.addAll(lista);
					
					Type type = new Type(Tipo.ARRAY,b.getTipoArray());
					type.setValor(new Arreglo(a,aleft,aright,b.getTipoArray())); 
					RESULT = type;
				}
				else {
					Type type = new Type(Tipo.ARRAY,b.getTipo());
					type.setValor(new Arreglo(a,aleft,aright,b.getTipo())); 
					RESULT = type;
				}

			:}
		   ;

dimensionList ::= dimensionList:a COMA dimension:b                  {: RESULT = a; RESULT.addLast(b); :} 
			    | dimension:a 										{: RESULT = new LinkedList<>(); RESULT.addLast(a);	:}
			    ;

dimension ::= expresion:a PNT PNT  expresion:b 						{: RESULT = new Dimension(a,b,aleft,aright); :} 
			| ID:a 													{: RESULT = new Dimension(a.toString(),aleft,aright); :} 
			;

//---------------------------------------------- REGISTROS ------------------------------------------------------------------------------
insRegistro ::= RECORD listaAtributos:a END   				{: RESULT = new Type("",Tipo.REGISTRO); RESULT.setValor(new Registro(a,aleft,aright)); :}
			   ;

listaAtributos ::= listaAtributos:a ID:b DSPUNTOS tipoDato:c PNTCOMA			{: RESULT = a; RESULT.addLast(new Atributo(b.toString(),c)); :}
				 | ID:a DSPUNTOS tipoDato:b PNTCOMA						        {: RESULT = new LinkedList<>(); RESULT.addLast(new Atributo(a.toString(),b)); :}
				 ; 

insFree ::= FREE PARIZQ ID:a PARDER 					{: RESULT = new Free(a.toString(),aleft,aright); :}
		  ;

//------------------------------------------------ LISTAS DE ID -------------------------------------------------------------------------
listaID ::= listaID:a COMA ID:b 						{: RESULT = a; RESULT.addLast(b.toString().toLowerCase()); :}
	      | ID:a 									    {: RESULT = new LinkedList<String>(); RESULT.addLast(a.toString()); :}
	      ;


expresion ::= expresion:a MAS expresion:b 											 {: RESULT = new Expresion(a,b,Operacion.SUMA,aleft,aright);  :} 
			| expresion:a MENOS expresion:b 										 {: RESULT = new Expresion(a,b,Operacion.RESTA,aleft,aright); :}
			| expresion:a POR expresion:b 										     {: RESULT = new Expresion(a,b,Operacion.MULTIPLICACION,aleft,aright); :}
			| expresion:a DIVIDIDO expresion:b 										 {: RESULT = new Expresion(a,b,Operacion.DIVISION,aleft,aright); :}	 
			| expresion:a MODULO expresion:b 										 {: RESULT = new Expresion(a,b,Operacion.MODULO,aleft,aright); :}
			| expresion:a POTENCIA expresion:b 										 {: RESULT = new Expresion(a,b,Operacion.POTENCIA,aleft,aright); :}
			| expresion:a MAYOR expresion:b 										 {: RESULT = new Expresion(a,b,Operacion.MAYOR,aleft,aright); :}
			| expresion:a MENOR expresion:b 										 {: RESULT = new Expresion(a,b,Operacion.MENOR,aleft,aright); :}
			| expresion:a MAYORIGUAL expresion:b 								     {: RESULT = new Expresion(a,b,Operacion.MAYORIGUAL,aleft,aright); :}
			| expresion:a MENORIGUAL expresion:b 								     {: RESULT = new Expresion(a,b,Operacion.MENORIGUAL,aleft,aright); :}
			| expresion:a IGUAL expresion:b 								         {: RESULT = new Expresion(a,b,Operacion.IGUAL,aleft,aright); :}
			| expresion:a DIFERENTE expresion:b 								     {: RESULT = new Expresion(a,b,Operacion.DIFERENTE,aleft,aright); :}
			| expresion:a AND expresion:b 		  	 							     {: RESULT = new Expresion(a,b,Operacion.AND,aleft,aright); :}
			| expresion:a OR expresion:b 		  	 							     {: RESULT = new Expresion(a,b,Operacion.OR,aleft,aright); :}
			| expresion:a NAND expresion:b 		  	 							     {: RESULT = new Expresion(a,b,Operacion.NAND,aleft,aright); :}
			| expresion:a NOR expresion:b 		  	 							     {: RESULT = new Expresion(a,b,Operacion.NOR,aleft,aright); :}
		    |             NOT expresion:a 											 {: RESULT = new Expresion(a,null,Operacion.NOT,aleft,aright); :} %prec RNOT
		    |             MENOS expresion:a 									     {: RESULT = new Expresion(a,null,Operacion.NEGATIVO,aleft,aright); :} %prec UMENOS
		    | expresion:a LLAVEIZQ listaAcceso:b LLAVEDER 						     {: RESULT = new Expresion(a,Operacion.ACCESOARRAY,aleft,aright,b); :}
		    | PARIZQ expresion:a PARDER                                              {: RESULT = a; :}
			| llamadaFuncion:a 														 {: RESULT = a; :}
			| SIZEOF PARIZQ ID:a PARDER 											 {: RESULT = new Expresion(Operacion.SIZEOF,aleft,aright,a.toString(),null); :}
			| MALLOC PARIZQ expresion:a PARDER 										 {: LinkedList<Expresion> lista = new LinkedList<Expresion>(); lista.addLast(a); RESULT = new Expresion(Operacion.MALLOC,aleft,aright,"",lista); :}
			| LENGTH PARIZQ expresion:a PARDER 					 					 {: LinkedList<Expresion> lista = new LinkedList<Expresion>(); lista.addLast(a); RESULT = new Expresion(Operacion.LENGTH,aleft,aright,"",lista); :}
			| TOLOWERCASE PARIZQ expresion:a PARDER 					 			 {: LinkedList<Expresion> lista = new LinkedList<Expresion>(); lista.addLast(a); RESULT = new Expresion(Operacion.TOLOWERCASE,aleft,aright,"",lista); :}
			| TOUPPERCASE PARIZQ expresion:a PARDER 					 			 {: LinkedList<Expresion> lista = new LinkedList<Expresion>(); lista.addLast(a); RESULT = new Expresion(Operacion.TOUPPERCASE,aleft,aright,"",lista); :}
			| TRUNK PARIZQ expresion:a PARDER 					 			         {: LinkedList<Expresion> lista = new LinkedList<Expresion>(); lista.addLast(a); RESULT = new Expresion(Operacion.TRUNK,aleft,aright,"",lista); :}
			| ROUND PARIZQ expresion:a PARDER 					 			         {: LinkedList<Expresion> lista = new LinkedList<Expresion>(); lista.addLast(a); RESULT = new Expresion(Operacion.ROUND,aleft,aright,"",lista); :}
			| REPLACE PARIZQ expresion:a COMA expresion:b PARDER 				     {: RESULT = new Expresion(a,b,Operacion.REPLACE,aleft,aright); :}
			| EQUALS PARIZQ expresion:a COMA expresion:b PARDER 					 {: RESULT = new Expresion(a,b,Operacion.EQUALS,aleft,aright); :}
			| ENTERO:a 																 {: RESULT = new Expresion(a,Tipo.INT,aleft,aright); :}
			| DECIMAL:a 															 {: RESULT = new Expresion(a,Tipo.DOUBLE,aleft,aright); :}
			| CHAR:a 																 {: RESULT = new Expresion(a,Tipo.CHAR,aleft,aright); :}
			| CADENA:a 															     {: RESULT = new Expresion(a,Tipo.STRING,aleft,aright); :}
			| TRUE:a															     {: RESULT = new Expresion(a,Tipo.BOOLEAN,aleft,aright); :}
			| FALSE:a 															     {: RESULT = new Expresion(a,Tipo.BOOLEAN,aleft,aright); :}
			| ID:a 															         {: RESULT = new Expresion(a,Tipo.ID,aleft,aright); :}
			| NIL:a 															     {: RESULT = new Expresion(a,Tipo.NULL,aleft,aright); :}
			| CHARAT PARIZQ expresion:a COMA expresion:b PARDER 					 {: RESULT = new Expresion(a,b,Operacion.CHARAT,aleft,aright); :}
			| expresion:a PNT ID:b 													 {: RESULT = new Expresion(a,null,Operacion.ACCESOID,aleft,aright,b.toString()); :}
			;


llamadaFuncion ::= ID:a PARIZQ listaAcceso:b PARDER 									 {: RESULT = new Expresion(Operacion.LLAMADA,aleft,aright,a.toString(),b); :}
				 | ID:a PARIZQ  PARDER 										 			 {: RESULT = new Expresion(Operacion.LLAMADA,aleft,aright,a.toString(),new LinkedList<>()); :}
				 ;
			

//----------------------------------------------------- INSTRUCCIONES DEL CUERPO PRINCIPAL --------------------------------------------------

cuerpoPrincipal ::= BEGIN instruccionesPrincipal:a END PNT                   {: RESULT = a; :}
				  | BEGIN END PNT 											 {: RESULT = new LinkedList<>(); :}
				  ;

instruccionesPrincipal ::= instruccionesPrincipal:a instruccionPrincipal:b       {: RESULT = a; RESULT.addLast(b);  :}
						 | instruccionPrincipal:a 								 {: RESULT = new LinkedList<>(); RESULT.addLast(a); :}
						 ;

instruccionPrincipal ::= insWrite:a PNTCOMA 										{: RESULT = a; :}
					   | insAsignacion:a PNTCOMA 								    {: RESULT = a; :}
					   | ifSuperior:a 												{: RESULT = a; :}
					   | insSwitch:a PNTCOMA 										{: RESULT = a; :}
					   | insWhile:a  		 										{: RESULT = a; :}
					   | insRepeat:a PNTCOMA 										{: RESULT = a; :}
					   | insFor:a  	    											{: RESULT = a; :}
					   | insRead:a PNTCOMA 											{: RESULT = a; :}
					   | insFree:a PNTCOMA 											{: RESULT = a; :}
					   | llamadaFuncion:a PNTCOMA 									{: RESULT = a; :}
					   | insBreak:a PNTCOMA 										{: RESULT = a; :}
					   | insContinue:a PNTCOMA 										{: RESULT = a; :}
					   | insExit:a PNTCOMA 										    {: RESULT = a; :}
 					   ;
					   
instruccionOneLine ::=  insWrite:a PNTCOMA 											{: RESULT = a; :}
					  | insAsignacion:a PNTCOMA 								    {: RESULT = a; :}
					  | insRead:a PNTCOMA 											{: RESULT = a; :}
					  | insFree:a PNTCOMA 											{: RESULT = a; :}
					  | llamadaFuncion:a PNTCOMA 									{: RESULT = a; :}
					  | insBreak:a PNTCOMA 											{: RESULT = a; :}
					  | insContinue:a PNTCOMA 										{: RESULT = a; :}
					  | insExit:a PNTCOMA 											{: RESULT = a; :}
 					  ;

//---------------------------------------------------------------- WRITE -----------------------------------------------------------------------

insWrite ::= WRITELN PARIZQ listaExpresiones:a PARDER 							{: RESULT = new Writeln(a,true); :}
		   | WRITE PARIZQ listaExpresiones:a PARDER 							{: RESULT = new Writeln(a,false); :}
		   ;

listaExpresiones ::= listaExpresiones:a COMA expresion:b  				{: RESULT = new Expresion(a,b,Operacion.SUMA,aleft,aright);	:}
				   | expresion:a 										{: RESULT = a; :}
				   ;


//--------------------------------------------------------------- ASIGNACION -------------------------------------------------------------------

insAsignacion ::= ID:a DSPUNTOS IGUAL expresion:b 										            {: RESULT = new Asignacion(a.toString(),b,aleft,aright); :}
			    | expresion:a LLAVEIZQ listaAcceso:b LLAVEDER DSPUNTOS IGUAL expresion:c 			{: RESULT = new AccesoArreglo(a,c,b,aleft,aright); :}
			    | expresion:a PNT ID:b DSPUNTOS IGUAL expresion:c 									{: RESULT = new AccesoRegistro(a,c,b.toString(),aleft,aright); :}
                ;

listaAcceso ::= listaAcceso:a COMA expresion:b  				    {: RESULT = a; RESULT.addLast(b); :}
			  | expresion:a 										{: RESULT = new LinkedList<>(); RESULT.addLast(a); :}
			  ;



//-------------------------------------------------------- IF / ELSE IF / ELSE --------------------------------------------------------------
ifSuperior ::= IF expresion:a THEN insIf:b  						 {: LinkedList<IF> lista = new LinkedList<>(); lista.addLast(new IF(a,aleft,aright,b)); RESULT = new IfSuperior(lista,aleft,aright);  :}
			 | IF expresion:a THEN insIf:b ELSE insIf:c       		 {: LinkedList<IF> lista = new LinkedList<>(); lista.addLast(new IF(a,aleft,aright,b)); lista.addLast(new IF(null,bleft,bright,c));  RESULT = new IfSuperior(lista,aleft,aright); :}
			 | IF expresion:a THEN insIf:b ELSE ifSuperior:c 		 {: LinkedList<IF> lista = new LinkedList<>(); lista.addLast(new IF(a,aleft,aright,b)); lista.addAll(c.getListado()); RESULT = new IfSuperior(lista,aleft,aright); :}
		     
			 ;

insIf ::= BEGIN instruccionesPrincipal:a END PNTCOMA 					{: RESULT = new LinkedList<>(); RESULT.addAll(a); :}
		| instruccionOneLine:a 										    {: RESULT = new LinkedList<>(); RESULT.addLast(a); :}
		; 

//---------------------------------------------------------- SWITCH -----------------------------------------------------------------------
insSwitch ::= CASE expresion:a OF listaCase:b insDefault:c END                      {: b.addLast(c); RESULT = new SWITCH(b,aleft,aright,a); :} 
			| CASE expresion:a OF listaCase:b END 									{: RESULT = new SWITCH(b,aleft,aright,a); :}	
			;

listaCase ::= listaCase:a insCase:b 												{: RESULT = a; RESULT.addLast(b); :} 
			| insCase:a 															{: RESULT = new LinkedList<>(); RESULT.addLast(a); :}
			;

insCase ::= listaAcceso:a DSPUNTOS instruccionPrincipal:b  			    {: LinkedList<Instruccion> lista = new LinkedList<>(); lista.addLast(b); RESULT = new CASE(a,lista,aleft,aright); :}
          ;

insDefault ::= DEFAULT DSPUNTOS instruccionPrincipal:b					{: LinkedList<Instruccion> lista = new LinkedList<>(); lista.addLast(b); RESULT = new CASE(null,lista,bleft,bright); :}
             ;



//------------------------------------------------- WHILE -----------------------------------------------------------------------------------
insWhile ::= WHILE expresion:a DO insOneLine:b 				{: RESULT = new WHILE(a,b,aleft,aright); :}
		   ;


//------------------------------------------------------------ REPEAT ----------------------------------------------------------------------
insRepeat ::= REPEAT instruccionesPrincipal:a UNTIL expresion:b 					{: RESULT = new REPEAT(b,a,aleft,aright); :}
  			;


//-------------------------------------------------------------- FOR ----------------------------------------------------------------------
insFor ::= FOR ID:a DSPUNTOS IGUAL expresion:b TO expresion:c DO insOneLine:d			{: RESULT = new FOR(a.toString(),b,c,d,aleft,aright,true); :} 
		 | FOR ID:a DSPUNTOS IGUAL expresion:b DOWNTO expresion:c DO insOneLine:d 		{: RESULT = new FOR(a.toString(),b,c,d,aleft,aright,false); :}
		 ;	



insOneLine ::= BEGIN instruccionesPrincipal:a END PNTCOMA 					{: RESULT = a; :}
		    | instruccionPrincipal:a 										{: RESULT = new LinkedList<>(); RESULT.push(a); :}
		    ;

//------------------------------------------------------------- READ -----------------------------------------------------------------------
insRead ::= READ PARIZQ ID:a PARDER 						{: RESULT = new Read(a.toString(),aleft,aright); :}
		  ;



//------------------------------------------------------------- BREAK ---------------------------------------------------------------------
insBreak ::= BREAK:a 						{: RESULT = new Break(aleft,aright); :}
	       ;


//------------------------------------------------------------- CONTINUE ------------------------------------------------------------------
insContinue ::= CONTINUE:a 					{: RESULT = new Continue(aleft,aright); :}
			  ;


//------------------------------------------------------------ EXIT -----------------------------------------------------------------------
insExit ::= EXIT:a 						{: RESULT = new Exit(aleft,aright); :}
		  ;